
&НаКлиенте
Процедура ПутьКДаннымНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	ДиалогВыбораФайла = новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ПутьКДаннымНачалоВыбораЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ПутьКДаннымНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры)  Экспорт 
	если ВыбранныеФайлы = Неопределено тогда
		Возврат;
	КонецЕсли;
	ПутьКДанным = ВыбранныеФайлы[0];
КонецПроцедуры // ПутьКДаннымНачалоВыбора()


&НаКлиенте
Процедура ЗагрузитьДанныеЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		ЗагрузитьДанныеНаСервере(Адрес, ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеНаСервере(Адрес, ДополнительныеПараметры)
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ПрочитатьИЗагрузить(ИмяВременногоФайла, ДополнительныеПараметры);
	
	Файл = Новый Файл(ИмяВременногоФайла);
	
	Если Файл.Существует() Тогда
		УдалитьФайлы(ИмяВременногоФайла);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИЗагрузить(ИмяВременногоФайла, ДополнительныеПараметры) 
	Читатель = Новый ЧтениеJSON;
	
	Читатель.ОткрытьФайл(ИмяВременногоФайла);

	НовыйМатериал = Неопределено;
	НужноЗаписатьНовыйМатериал = Ложь;
	
	Пока Читатель.Прочитать() Цикл
		Если Читатель.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
			Если Читатель.ТекущееЗначение = "material" Тогда
				НужноЗаписатьНовыйМатериал = Истина;
				
				НовыйМатериал = Справочники.Номенклатура.СоздатьЭлемент();
				
				НовыйМатериал.Родитель = ДополнительныеПараметры.ВыбраннаяГруппа;
				
			ИначеЕсли Читатель.ТекущееЗначение = "key" Тогда
				Читатель.Прочитать();
				
				НовыйМатериал.Код = Читатель.ТекущееЗначение;
				
			ИначеЕсли Читатель.ТекущееЗначение = "name" Тогда
				Читатель.Прочитать();
				
				НовыйМатериал.Наименование = Читатель.ТекущееЗначение;

			КонецЕсли;
			
		ИначеЕсли Истина
			И Читатель.ТипТекущегоЗначения = ТипЗначенияJSON.КонецОбъекта
			И НужноЗаписатьНовыйМатериал
			Тогда

			НужноЗаписатьНовыйМатериал = Ложь;
			
			ИскомыйМатериал = Справочники.Номенклатура.НайтиПоКоду(НовыйМатериал.Код);
			Если Ложь
				Или ИскомыйМатериал = Неопределено
				Или ИскомыйМатериал = Справочники.Номенклатура.ПустаяСсылка()
				Тогда
			
				НовыйМатериал.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Читатель.Закрыть();
КонецПроцедуры


&НаКлиенте
Процедура Загрузить(Команда)
	АдресФайлаВоВременномХранилище = "";
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("ВыбраннаяГруппа", ГруппаЗагрузки);
	
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьДанныеЗавершение", ЭтотОбъект, ДополнительныеПараметры), АдресФайлаВоВременномХранилище, ПутьКДанным, Ложь);

КонецПроцедуры

